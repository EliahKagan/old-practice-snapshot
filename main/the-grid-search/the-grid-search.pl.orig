#!/usr/bin/env perl

use strict;
use warnings;
use Carp qw(croak);

sub read_row {
    my ($width) = @_;

    chomp(my $line = <>);
    length $line == $width or croak 'wrong length line in text or pattern';
    return $line;
}

sub read_grid {
    my ($height, $width) = split q{ }, <>;
    my @lines = map { read_row($width) } (1..$height);
    return ($height, $width, @lines);
}

sub run {
    my ($text_height, $text_width, @text_lines) = read_grid();
    my ($pattern_height, $pattern_width, @pattern_lines) = read_grid();

    my $delta = $text_width - $pattern_width;
    if ($delta < 0) { return 0 }

    my $text = join q{}, @text_lines;
    my $pattern = join ".{$delta}", @pattern_lines;
    return $text =~ /$pattern/msx;
}

for (1..<>) { print run() ? "YES\n" : "NO\n" }
