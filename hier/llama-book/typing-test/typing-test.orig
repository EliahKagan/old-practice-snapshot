#!/usr/bin/env perl

use strict;
use warnings;
use autodie;

sub load_words {
    my $path = shift;
    my @words;

    open my $in, '<', $path;
    while (<$in>) {
        if (m{^([[:lower:]]+)   # an all-lowercase word
                (?:/|$)}ax) {   # possibly followed by Hunspell metadata
            push @words, $1;
        }
    }
    close $in;

    return @words;
}

my @words = load_words '/usr/share/hunspell/en_US.dic';
my $count = scalar @words;
my $nreps = 10;

my %history;
my $errors = 0;

for my $i (1 .. $nreps) {
    my $word = $words[int rand $count];
    if (exists $history{$word}) { redo }
    $history{$word} = 1;

    print "Type the word \"$word\":  ";
    (my $try = <STDIN>) =~ s/\s//g;

    if ($try ne $word) {
        print "Sorry, that's not right!\n";
        ++$errors;
        redo;
    }
}

printf "You've completed the test, with %d error%s.\n",
       $errors, ($errors == 1 ? q{} : q{s});
